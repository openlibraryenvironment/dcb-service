import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
	repositories {
		mavenCentral()
	}
}

plugins {
  id "io.micronaut.graalvm" version "$micronautGradleVersion"
	id("com.github.johnrengelman.shadow") version "8.1.1"
	// See https://plugins.gradle.org/plugin/io.micronaut.application
	id("io.micronaut.application") version "$micronautGradleVersion"
	id("io.micronaut.test-resources") version "$micronautGradleVersion"
	// id("io.micronaut.aot") version "$micronautGradleVersion"
	id('jacoco')
	id 'org.olf.dcb-bom'
	id 'service.k_int.micronaut-multi'
  // Per https://guides.micronaut.io/latest/adding-commit-info-gradle-java.html
  id "com.gorylenko.gradle-git-properties" version "2.5.0"
  id('com.jaredsburrows.license') version '0.9.8'
}

//version = "0.1"
group = "org.olf.dcb"

repositories {
	mavenCentral()
}

ext {
  BASE_IMAGE = project.findProperty('BASE_IMAGE') ?: 'eclipse-temurin:21-jre-alpine'
}


dependencies {
	// This should likely always be first in the annotations list
	compileOnly 'org.projectlombok:lombok'

	annotationProcessor("org.projectlombok:lombok")
	// annotationProcessor("io.micronaut:micronaut-inject")
	annotationProcessor("io.micronaut:micronaut-inject-java")
	annotationProcessor("io.micronaut.data:micronaut-data-processor")
	annotationProcessor("io.micronaut.serde:micronaut-serde-processor")

	annotationProcessor("io.micronaut:micronaut-http-validation")
	annotationProcessor("io.micronaut.openapi:micronaut-openapi")
	annotationProcessor("io.micronaut.security:micronaut-security-annotations")
	annotationProcessor("io.micronaut.validation:micronaut-validation-processor")
	annotationProcessor('io.micronaut:micronaut-graal')
  annotationProcessor("io.micronaut.micrometer:micronaut-micrometer-annotation")

	compileOnly("org.graalvm.nativeimage:svm")
	compileOnly("io.micronaut:micronaut-inject-java")



	// Implementation
	implementation("io.micronaut.xml:micronaut-jackson-xml")
	implementation("io.micronaut.serde:micronaut-serde-jackson")
	implementation("io.micronaut.security:micronaut-security-jwt")
	implementation("io.micronaut:micronaut-http-client")
	implementation("io.micronaut:micronaut-management")
	implementation("io.micronaut.cache:micronaut-cache-caffeine")
	//	implementation("io.micronaut.elasticsearch:micronaut-elasticsearch")
	implementation("io.micronaut.reactor:micronaut-reactor")
//	implementation('io.projectreactor.addons:reactor-extra')
	// implementation platform('io.projectreactor:reactor-bom')
	implementation 'io.projectreactor.addons:reactor-extra'

	implementation("io.micronaut.reactor:micronaut-reactor-http-client")
	implementation("io.swagger.core.v3:swagger-annotations")
	implementation("jakarta.annotation:jakarta.annotation-api")
	// implementation("io.micronaut:micronaut-validation")
	implementation("io.micronaut.validation:micronaut-validation")

	implementation("io.micronaut.elasticsearch:micronaut-elasticsearch")


	// implementation("org.apache.logging.log4j:log4j-api")
	// implementation("org.apache.logging.log4j:log4j-core")

	// implementation("io.micronaut:micronaut-jackson-databind") // Not using Jackson for binding.
	// implementation("io.micronaut.xml:micronaut-jackson-xml")
	implementation 'org.marc4j:marc4j'

  implementation 'org.opensearch.client:opensearch-java'
  implementation 'org.opensearch.client:opensearch-rest-client'
	// implementation 'org.opensearch.client:opensearch-java'
	// implementation 'org.opensearch.client:opensearch-rest-client'

	implementation("ch.qos.logback:logback-core")
	implementation("ch.qos.logback:logback-classic")

	// These are needed to address critical CVEs that block deployment
	implementation("io.netty:netty-handler")
	implementation("org.apache.httpcomponents.client5:httpclient5")
	implementation("commons-beanutils:commons-beanutils")
	
	implementation 'info.debatty:java-string-similarity'

  runtimeOnly("ch.qos.logback.contrib:logback-json-classic")
  runtimeOnly("ch.qos.logback.contrib:logback-jackson")
  // runtimeOnly("ch.qos.logback.contrib:logback-json-classic:0.1.5")

  // Mostly for AWS Cloudwatch log formatting - enabled by default, see logback.xml for how to change
  // Commenting out - tests are failing and this seems to be the reason
  // implementation("io.micronaut.aws:micronaut-aws-cloudwatch-logging")

	runtimeOnly("org.slf4j:log4j-over-slf4j")

	//data
	implementation("io.micronaut.r2dbc:micronaut-r2dbc-core")
	implementation("io.micronaut.data:micronaut-data-processor")
	implementation("io.micronaut.data:micronaut-data-r2dbc")
	implementation("io.micronaut.data:micronaut-data-jdbc")
	implementation("io.micronaut.sql:micronaut-jdbc-hikari")
	// Introduced in DCB-1302 because of this issue https://github.com/micronaut-projects/micronaut-security/issues/1468
	implementation("io.micrometer:context-propagation")

  implementation("io.micronaut.problem:micronaut-problem-json")

	runtimeOnly("org.postgresql:postgresql")

	// Without this dependency, testcontainer injection seems not to happen at all.
	// Wonder if this is because the bean that uses PG is a r2dbc service
	runtimeOnly("org.postgresql:r2dbc-postgresql")


		// testRuntimeOnly("io.micronaut.testresources:micronaut-test-resources-client")

	// Uncommmenting this dependency results in
	// testresources Micronaut Message: Could not resolve placeholder ${auto.test.resources.r2dbc.datasources.default.options.protocol}
	//
	runtimeOnly("io.r2dbc:r2dbc-pool")

	implementation("io.micronaut.flyway:micronaut-flyway")
	runtimeOnly("org.flywaydb:flyway-database-postgresql")

        // II: These were commented out but unsure why - needed for observability on prod systems
	// Metrics (Prometheus)
	implementation "io.micronaut.micrometer:micronaut-micrometer-registry-prometheus"

	// Also required to enable endpoint
	implementation "io.micronaut:micronaut-management"

	implementation 'info.debatty:java-string-similarity'
  implementation 'com.opencsv:opencsv'

	implementation("jakarta.persistence:jakarta.persistence-api")
	implementation("org.apache.lucene:lucene-queryparser")

	implementation 'com.hazelcast:hazelcast'

	// Tests
	testAnnotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor "io.micronaut:micronaut-inject-java"
	testCompileOnly 'org.projectlombok:lombok'
	testCompileOnly 'io.micronaut:micronaut-inject-java'

	/**/
	testAnnotationProcessor("io.micronaut.serde:micronaut-serde-processor")

	//*/
	testAnnotationProcessor("io.micronaut.data:micronaut-data-processor")
	testAnnotationProcessor("io.micronaut.validation:micronaut-validation-processor")

	testAnnotationProcessor("io.micronaut:micronaut-http-validation")
	testAnnotationProcessor("io.micronaut.openapi:micronaut-openapi")
	testAnnotationProcessor("io.micronaut.security:micronaut-security-annotations")
	testAnnotationProcessor('io.micronaut:micronaut-graal')
	/**/


	testImplementation (platform("org.junit:junit-bom"))
	testImplementation("org.junit.jupiter:junit-jupiter-api")
	testImplementation("io.micronaut.test:micronaut-test-junit5")
	testImplementation("org.junit.jupiter:junit-jupiter-engine")
	testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")

	testImplementation('org.mock-server:mockserver-junit-jupiter')
	testImplementation('org.junit.jupiter:junit-jupiter-params')

	testImplementation('org.mockito:mockito-core')
	testImplementation('org.mockito:mockito-junit-jupiter')
	testImplementation('org.hamcrest:hamcrest')
	testImplementation('org.awaitility:awaitility')
	testImplementation('io.projectreactor:reactor-test')

	// testImplementation(platform("org.testcontainers:testcontainers-bom"))

	testRuntimeOnly("org.testcontainers:postgresql")

	implementation("io.micronaut.graphql:micronaut-graphql")
	implementation("com.graphql-java:graphql-java-extended-scalars" )

	implementation("jakarta.persistence:jakarta.persistence-api")
	implementation("org.apache.lucene:lucene-queryparser")

	runtimeOnly("org.yaml:snakeyaml")

//  implementation("io.projectreactor.tools:blockhound:1.0.13.RELEASE")

	//aotPlugins platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
	//aotPlugins ("io.micronaut.security:micronaut-security-aot")

	// For graphviz
	implementation 'guru.nidi:graphviz-java-all-j2v8'
	// implementation 'org.graalvm.js:js:23.0.3'

	// Different distributed configuration engines
  implementation("io.micronaut.aws:micronaut-aws-secretsmanager")
  implementation("io.micronaut.aws:micronaut-aws-sdk-v2")  // Required for AWS SDK v2

	// implementation project(':openrs-core')
}

application {
	mainClass.set("org.olf.dcb.Application")
}

run {
	systemProperties([
		'micronaut.environments': 'development'
	])
}

java {
	sourceCompatibility = JavaVersion.toVersion("17")
	targetCompatibility = JavaVersion.toVersion("17")
}

micronaut {
	runtime("netty")
	testRuntime("junit5")
	version "4.4.3"
	processing {
		incremental(true)
		annotations("org.olf.dcb.*", "services.k_int.*")
	}
//	aot {
//    // Please review carefully the optimizations enabled below
//    // Check https://micronaut-projects.github.io/micronaut-aot/latest/guide/ for more details
//      optimizeServiceLoading = true
//      convertYamlToJava = true
//      precomputeOperations = true
//      cacheEnvironment = false
//      optimizeClassLoading = true
//      deduceEnvironment = false
//      optimizeNetty = true
//  }
}

final Set<String> jacocoExcludes = [
	'**/Application.class',
	'services/k_int/**/*',
	'**/model/**/*',
	// Ingest happens at a different client level, so safely exclude from report.
	'org/olf/dcb/ingest/sierra/**/*',	'org/olf/dcb/ingest/gokb/**/*']

tasks {
	final Map<String, String> labels = [
		'maintainer': 'Steve Osguthorpe <steve.osguthorpe@k-int.com>',
		'version'   : project.hasProperty('versionWithMeta') ? project.getProperty('versionWithMeta') : project.getProperty('version')
	]

	// N.B. short term fix - don't check for cert revocation
	dockerfile {
		baseImage project.property('BASE_IMAGE')
		label(labels)
		environmentVariable 'JAVA_OPTIONS',
			'-XX:+UseContainerSupport -XX:MinRAMPercentage=50.0 -XX:MaxRAMPercentage=80.0 -XX:InitialRAMPercentage=50.0 -XX:+PrintFlagsFinal -Dcom.sun.net.ssl.checkRevocation=false' +
      ' --add-exports=java.base/jdk.internal.ref=ALL-UNNAMED' +
      ' --add-opens=java.base/java.lang=ALL-UNNAMED' +
      ' --add-opens=java.base/sun.nio.ch=ALL-UNNAMED' +
      ' --add-opens=java.management/sun.management=ALL-UNNAMED' +
      ' --add-opens=jdk.management/com.sun.management.internal=ALL-UNNAMED'

		environmentVariable 'JAVA_TOOL_OPTIONS', '$JAVA_OPTIONS $JAVA_TOOL_OPTIONS $ADDITIONAL_JAVA_OPTIONS'
		entryPoint('java', '-server', '-jar', '/home/app/application.jar')
	}

	dockerfileNative {
		label(labels)
                // graalArch.set(org.apache.tools.ant.taskdefs.condition.Os.isArch("aarch64") ? "aarch64" : "amd64")
                // graalArch.set("x64")
                // graalArch.set("compatibility")
                // Apparently graal defaults to x86-64-v3 - dev server is v2 so targetting v2
                // graalArch.set("x86-64-v2")
	}

	boolean isPre = project.hasProperty('versionPreRelease') && !project.property('versionPreRelease').equals('unspecified') &&
		("${project.property('versionPreRelease')}".trim() != "")

	// Always add the version including any specified prerelease
	final Set<String> tags = [
		'version'
	].findResults {
		(project.hasProperty(it) && !project.property(it).equals('unspecified')) ? project.property(it).toString().trim() : null
	}

	// Build up a list of tags we should apply
	tags.addAll( [
		'versionMajor',
		'versionMajorMinor'
	].findResults {

		final String tag = (project.hasProperty(it) && !project.property(it).equals('unspecified')) ? project.property(it).toString().trim() : null
		if (tag == null) return null

		// Append the next keyword if this is a tag.
		if (isPre) {
			// Trim the prerelease off and just add next for partial tags
			final String toTrim = project.property('versionPreRelease');
			return "${tag - toTrim}-next"
		}

		tag
	})

	// If this is a pre-release (including Snapshots) then add "next" tag. Otherwise, use latest
	tags << (isPre ? 'next' : 'latest')

	final imageAndRepo = "${System.env.DOCKER_IMAGE ?: 'docker.libsdev.k-int.com/' + project.name.toLowerCase()}"

	Set<String> imageNames = tags.collect { "${imageAndRepo}:${it}" }
	Set<String> nativeImageNames = tags.collect { "${imageAndRepo}:native-${it}" }

	dockerBuild {
		images = imageNames
	}

	dockerBuildNative {
		images = nativeImageNames
	}


	jacocoTestReport {
		dependsOn test

		reports {
			xml.required = false
			csv.required = true
		}

		afterEvaluate {
			classDirectories.setFrom(files(classDirectories.files.collect {
	      fileTree(dir: it, exclude: jacocoExcludes)
			}))
		}
	}
	
	jacocoTestCoverageVerification {

		afterEvaluate {
			classDirectories.setFrom(files(classDirectories.files.collect {
				fileTree(dir: it, exclude: jacocoExcludes)
			}))
		}

		violationRules {
			rule {
				limit {
					minimum = 0.7 // 70% coverage
				}
			}
		}
	}
}

graalvmNative {
		toolchainDetection = false
		metadataRepository {
      // Force the version of the metadata
      moduleToConfigVersion.put("com.hazelcast:hazelcast", "5.2.1")
    }
    binaries {
        main {
        		// Uncomment quickBuild when testing in development only
        		// quickBuild = true
            buildArgs.add('--verbose')
            buildArgs.add('-march=x86-64-v2')
            buildArgs.add('-H:+AddAllCharsets')
            buildArgs.add('--initialize-at-build-time=ch.qos.logback.contrib.jackson.JacksonJsonFormatter,org.slf4j.LoggerFactory,ch.qos.logback,kotlin.coroutines.intrinsics.CoroutineSingletons')
            buildArgs.add('--add-opens=java.base/java.nio=ALL-UNNAMED')
            buildArgs.add('--add-opens=java.base/jdk.internal.misc=ALL-UNNAMED')
            buildArgs.add('--add-opens=java.base/jdk.internal.ref=ALL-UNNAMED')
            buildArgs.add('--trace-class-initialization=ch.qos.logback.classic.Logger')
            buildArgs.add('--trace-object-instantiation=ch.qos.logback.core.AsyncAppenderBase$Worker')
            buildArgs.add('--initialize-at-run-time=io.netty')
            // buildArgs.add('-H:ReflectionConfigurationFiles=/home/app/resources/META-INF/native-image/reflect-config.json')
            // buildArgs.add('--exact-reachability-metadata')
        }
		}
}


tasks.withType(ShadowJar) {
    setZip64(true)
}

tasks.withType(Test).configureEach {
    jvmArgs '--add-exports=java.base/jdk.internal.ref=ALL-UNNAMED',
            '--add-opens=java.base/java.lang=ALL-UNNAMED',
            '--add-opens=java.base/sun.nio.ch=ALL-UNNAMED',
            '--add-opens=java.management/sun.management=ALL-UNNAMED',
            '--add-opens=jdk.management/com.sun.management.internal=ALL-UNNAMED',
            '-Dio.netty.leakDetectionLevel=paranoid'
}

def oderedAnnotationDeps = new HashMap<String, Integer>()

configurations.annotationProcessor { ap ->
	ap.incoming.afterResolve { resolution ->

		println ap.name
		int count = 0
    resolution.resolutionResult.allComponents.each	{ comp ->
    	comp.dependencies.each { dependency ->

    		if (ResolvedDependencyResult.class.isAssignableFrom( dependency.getClass() )) {
    			ResolvedDependencyResult resDep = dependency as ResolvedDependencyResult
    			final String key = "${resDep.selected}";
    			if (oderedAnnotationDeps.containsKey( key )) return;

	    	  //println dependency
	      	oderedAnnotationDeps[key] = count++
      	}
      }
    }
    // println oderedAnnotationDeps
  }
}

eclipse {
  factorypath {
  	file {
	  	whenMerged { fp ->

	  	  final String regex = '^.*\\/([^\\.\\/]+(\\.[^\\.\\/]+)+)\\/([^\\/]+)\\/(\\d+[^\\/]+)\\/.*$'
	  	  final String subst = '$1:$3:$4';

				// Change the enties
				List entries = fp.getEntries().sort { s1, s2 ->
				  final String sub1 = s1.canonicalPath.replaceAll( regex, subst )
				  final String sub2 = s2.canonicalPath.replaceAll( regex, subst )

					int val1 = oderedAnnotationDeps.get( sub1 ) ?: Integer.MAX_VALUE
					int val2 = oderedAnnotationDeps.get( sub2 ) ?: Integer.MAX_VALUE

					val1 <=> val2
				}

				fp.getEntries().each { fpent ->
					println fpent
				}
			}
		}
  }
}


licenseReport {
  // Generate reports
  generateCsvReport = false
  generateHtmlReport = true
  generateJsonReport = false
  generateTextReport = false

  // Copy reports - These options are ignored for Java projects
  copyCsvReportToAssets = false
  copyHtmlReportToAssets = true
  copyJsonReportToAssets = false
  copyTextReportToAssets = false

  // Ignore licenses for certain artifact patterns
  ignoredPatterns = []

  // Show versions in the report - default is false
  showVersions = true
}

gitProperties {
    // if .git directory is on the same level as the root project
    dotGitDirectory = project.rootProject.layout.projectDirectory.dir(".git")
    failOnNoGitDirectory = false

    // if .git directory is in a different location
    // dotGitDirectory = "${project.rootDir}/../.git"
}

test {
	// Reduce noise in CI logs by disabling per-test XML reports
	reports.junitXml.outputPerTestCase = false
	testLogging {
		exceptionFormat = 'short'
		showStandardStreams = false
	}
}
